ext {

    getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }.memoize()

        getGitTime = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'reflog', '-1', '--format=%cs'
            standardOutput = stdout
        }
        def time = stdout.toString().trim().replaceAll('-', '')
        return time
    }.memoize()

    getGitVersion = { ->
        return getGitTime() + '-' + getGitHash()
    }.memoize()

    getJibTags = { ->
        tags = []
        if (System.getenv("CI") == null) {
            tags.add("developer")
        } else {
            tags.add(getGitHash())
        }
        return tags
    }.memoize()

}

dependencies {

    implementation platform(libs.micrometerPlatform)
    implementation platform(libs.junit5Platform)

    pluginManager.withPlugin('java-test-fixtures') {
        testFixturesImplementation platform(libs.micrometerPlatform)
        testFixturesImplementation platform(libs.junit5Platform)
    }

    constraints {

        implementation libs.javax.annotationApi
        implementation libs.javax.injectApi
        implementation libs.javax.persistenceApi
        implementation libs.javax.restApi
        implementation libs.javax.validationApi

        pluginManager.withPlugin('java-test-fixtures') {

            testFixturesImplementation libs.javax.annotationApi
            testFixturesImplementation libs.javax.injectApi
            testFixturesImplementation libs.javax.persistenceApi
            testFixturesImplementation libs.javax.restApi
            testFixturesImplementation libs.javax.validationApi

        }

    }

}

configurations.all {

    resolutionStrategy {

        dependencySubstitution {
            substitute module('javax.annotation:javax.annotation-api') with module(libs.javax.annotationApi.get().toString())
            substitute module('javax.inject:javax.inject') with module(libs.javax.injectApi.get().toString())
            substitute module('javax.persistence:javax.persistence-api') with module(libs.javax.persistenceApi.get().toString())
            substitute module('javax.validation:validation-api') with module(libs.javax.validationApi.get().toString())
            substitute module('javax.ws.rs:javax.ws.rs-api') with module(libs.javax.restApi.get().toString())
        }

        eachDependency { DependencyResolveDetails details ->

            if (details.requested.group == 'com.google.guava' && details.requested.version.contains('android')) {
                details.useVersion details.requested.version.replace('android', 'jre')
                details.because 'we want the JRE version of Guava'
            }

        }

    }
}

pluginManager.withPlugin('jacoco') {
    jacoco {
        toolVersion = '0.8.8'
    }
}

pluginManager.withPlugin('java') {

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    [compileJava, compileTestJava]*.options.collect { options ->
        options.compilerArgs.add '-parameters'
        options.encoding = 'UTF-8'
    }

}

pluginManager.withPlugin('com.google.cloud.tools.jib') {

    jib {
        from {
            image = 'adoptopenjdk/openjdk11:alpine-jre'
        }
        to {
            tags = getJibTags()
        }
        container {
            appRoot = workingDirectory = '/app'
            entrypoint = ['./start.sh']
            ports = ['8080/tcp']
        }
        extraDirectories {
            paths {
                path {
                    from = file('src/main/jib/app')
                    into = '/app'
                }
            }
            permissions = [
                    '/app/start.sh': '755'
            ]
        }
    }

    project.afterEvaluate {
        // TARGET_IMAGE is set by the script `java/services/.scripts/uploadArtifact`
        if (System.getenv("TARGET_IMAGE") != null) {
            jib.to.image = System.getenv("TARGET_IMAGE")
        } else {
            jib.to.image = dockerImageName
        }
    }

}
